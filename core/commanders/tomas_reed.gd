class_name TomasReed
extends CommanderType
## Verdant League. An infantry doctrine: his foot units fight and capture above
## their price, his vehicles below theirs, so he wants a cheap wide army taking
## ground rather than an expensive one holding it. Popular Uprising can take a
## property outright in a single turn.
##
## Keyed on movement class rather than unit id: "foot units" is what the
## doctrine means, and a future walking unit should inherit it without an edit
## here.

@export var foot_classes: Array[StringName] = [TerrainType.FOOT, TerrainType.BOOT]
@export var foot_attack_pct: int = 15
## Negative on purpose: what the infantry bonus above costs him.
@export var vehicle_attack_pct: int = -10
@export var capture_pct: int = 20
## +100 doubles the chip, so 10 displayed HP takes a property in one turn.
@export var uprising_capture_pct: int = 100
@export var uprising_move_bonus: int = 1
## Build-list places his foot units are pulled up: a wide, cheap army is the
## doctrine, so the width keeps coming after the capture roster is filled.
@export var foot_build_bias: int = -5


func attack_bonus(_state: GameState, fight: Engagement) -> int:
	return foot_attack_pct if _is_foot(fight.attacker) else vehicle_attack_pct


func capture_bonus_pct(state: GameState, unit: Unit) -> int:
	var bonus := capture_pct
	if _is_active(state, unit.team):
		bonus += uprising_capture_pct
	return bonus


func move_bonus(state: GameState, unit: Unit) -> int:
	if not _is_active(state, unit.team) or not _is_foot(unit):
		return 0
	return uprising_move_bonus


## Uprising is measured in capture points, not damage, so it waits for a
## property his infantry can actually stand on this turn rather than for a
## fight it would contribute nothing to. Measured with the move the power itself
## grants, since a property one step out of reach is precisely what firing fixes.
func wants_power(state: GameState, team: int) -> bool:
	return _can_reach_capture(state, team, uprising_move_bonus)


## Production advice, keyed on movement class like the rest of the doctrine.
func build_bias(_state: GameState, _team: int, unit_type: UnitType) -> int:
	return foot_build_bias if unit_type.move_class in foot_classes else 0


func _is_foot(unit: Unit) -> bool:
	return unit.type.move_class in foot_classes
